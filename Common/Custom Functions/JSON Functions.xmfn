<?xml version="1.0" encoding="UTF-8"?><FMClipboard><Version><![CDATA[41011080]]></Version><Comment><![CDATA[v18+ only]]></Comment><SourceWindowName></SourceWindowName><Class>XMFN</Class><ClipVersion>1</ClipVersion><ClipTimeStamp>3637300875</ClipTimeStamp><ClipAuthor><![CDATA[gsbcs]]></ClipAuthor><clipboardAsText><![CDATA[JSON.array.appendKey( array )
JSON.array.count( array )
JSON.array.toList( array )
JSON.array.fromList( lst;type )
JSON.array.push( arr;val;type )
JSON.array.slice( array;start;numberOfValues )
JSON.defaultTo( jsonResult;default )
JSON.record.getData( Definition )
JSON.record.getFields( context )
JSON.setVariables( json )
JSON.getAvailableTimeBlocks( JSON.usedTimeBlocks )
GetEMLFieldsAsJSON( emlText )
JSONGetValues( json;path )]]></clipboardAsText><Clipboard><fmxmlsnippet type="FMObjectList"><CustomFunction id="14" functionArity="1" visible="True" parameters="array" name="JSON.array.appendKey"><Calculation>Let ([b=""
;	indices = JSONListKeys ( array ; "" )
;	next = Case ( Left ( indices ; 1 ) = "?" ; 0 ; GetAsNumber ( GetValue ( indices ; ValueCount ( indices ) ) ) + 1 )
];
next
)</Calculation></CustomFunction><CustomFunction id="58" functionArity="1" visible="True" parameters="array" name="JSON.array.count"><Calculation>Case ( IsEmpty ( array ) ; 0 ; Let ([b=""

;	keys	= JSONListKeys ( array ; "" )
;	cnt	= Case ( Left ( keys ; 1 ) = "?" ; 0 ; ValueCount ( keys ) )
	
];
cnt
))</Calculation></CustomFunction><CustomFunction id="61" functionArity="1" visible="True" parameters="array" name="JSON.array.toList"><Calculation>Case ( array = "[]" or Left ( array ; 1 ) = "?" ; "" ; JSONListValues ( array ; "" ) )</Calculation></CustomFunction><CustomFunction id="17" functionArity="2" visible="True" parameters="lst;type" name="JSON.array.fromList"><Calculation>Let ([b=""

;	$_array	= "[]"

;	template	= ";	$_array = JSON.array.push ( $_array ; " &amp; Case ( type = JSONNumber ; "{{value}}" ; Quote ( "{{value}}" ) ) &amp; " ; " &amp; type &amp; " )"
;	values = lst
;	pushes	= List.fillTemplate ( template ; lst )
;	str	= List (""
			; "Let ([b=\"\""
			; ";	$_array = \"[]\""
			;	pushes
			; "];"
			; "	$_array"
			; ")"
			)

;	res	= Evaluate ( str )
;	output	= $_array
;	$_array	= ""

];
output
)</Calculation></CustomFunction><CustomFunction id="16" functionArity="3" visible="True" parameters="arr;val;type" name="JSON.array.push"><Calculation>Let ([b=""
/*
;	arr	= "[]"
;	val	= "test"
;	type 	= JSONString
*/

;	keys	= JSONListKeys ( arr ; "" )
;	len	= ValueCount ( keys )

;	thisKey	= GetValue ( keys ; len )
;	nextKey	= Case ( IsEmpty ( thisKey ) ; 0 ; thisKey + 1 )

;	return	= JSONSetElement ( arr ; nextKey ; val ; type )

];
	return
)</Calculation></CustomFunction><CustomFunction id="2" functionArity="3" visible="True" parameters="array;start;numberOfValues" name="JSON.array.slice"><Calculation>Let ([b=""

/* Test values: Use in Data Viewer.  Comment out in Custom Function
;	array		= JSONSetElement ( "[]"
				;[ 0 ; List ( "this" ; Quote ( "is" ); Quote ( "the" ) ; "first" ) ; JSONString ]
				;[ 1 ; List ( "this" ; Quote ( "is" ); Quote ( "the" ) ; "second" ) ; JSONString ]
				;[ 2 ; List ( "this" ; Quote ( "is" ); Quote ( "the" ) ; "third" ) ; JSONString ]
				;[ 3 ; List ( "this" ; Quote ( "is" ); Quote ( "the" ) ; "fourth" ) ; JSONString ]
				;[ 4 ; List ( "this" ; Quote ( "is" ); Quote ( "the" ) ; "fifth" ) ; JSONString ]
				)
;	start		= 2
;	numberOfValues = 3
 */

/* Function */
;	arrayDelimiterWithQuotes	= Quote ( "," )
;	innerReturnHolder		= "~!@"
;	innerQuoteHolder		= "#$%"

;	innerReturnsPreserved	= Substitute ( array ; "\r" ; innerReturnHolder )
;	valuesListed		= JSONListValues ( innerReturnsPreserved ; "" )
;	innerQuotesPreserved	= Substitute ( valuesListed ; "\"" ; innerQuoteHolder )

;	valuesSliced		= MiddleValues ( innerQuotesPreserved ; start ; numberOfValues )
;	trailingReturnDropped	= Left ( valuesSliced ; Length ( valuesSliced ) - 1 )

;	restoredToJSONArray	= Case (0;0
					;	ValueCount ( trailingReturnDropped )
					;		Substitute ( "[\"{{entries}}\"]"
							;[ "{{entries}}" ; trailingReturnDropped ]
							;[ "¶" ; arrayDelimiterWithQuotes ]
							;[ innerReturnHolder ; "\r" ]
							;[ innerQuoteHolder ; "\\\"" ]
							)
					;	"[]"
					)
];
restoredToJSONArray
)</Calculation></CustomFunction><CustomFunction id="20" functionArity="2" visible="True" parameters="jsonResult;default" name="JSON.defaultTo"><Calculation>Case ( IsEmpty ( jsonResult ) or Left ( jsonResult ; 1 ) = "?" ; default ; jsonResult )</Calculation></CustomFunction><CustomFunction id="12" functionArity="1" visible="True" parameters="Definition" name="JSON.record.getData"><Calculation>/*
 * @author Steve Allen &lt;steven.allen@rcconsulting.com&gt;
 *    @gitHub steveAllen0112
 *
 * @purpose To contextually and consistently produce record data in JSON format
 *     Uses the Schema Definition provided to it (See `JSON.record.getFields()` as a way to produce that.)
 *     This prevents having to hard-code a CF for each table, and allows flexibility to be as consistent or not as needed at runtime.
 * 
 * @param *required Definition (object)
 *    @property *required _table (string) The name of the Table Occurrence to grab the data from.  Must be reachable in the context.
 *        Only required at the first level of the call, but must be present there or the CF will not work,
 *           because it's used to fully qualify the field definitions passed in the rest of the Schema Definition
 *    @properties *required {Schema-Definition} See the @return value on the `JSON.record.getFields()` function.
 *
 * @returns {record-data} (object)
 *     Exact structure will vary according to the Definition provided, but will match that definition's structure.
 */

Let ([b=""
;	debug = False

;	properties		= JSON.defaultTo ( JSONListKeys ( Definition ; "" ) ; "" )

;	hasTableDefinition	= ValueCount ( FilterValues ( properties ; "_table" ) ) &gt;= 1
;	isFieldDefinition		= ValueCount ( FilterValues ( properties ; List ( "field" ; "type" ; "reps" ) ) ) &gt;= 3 /* these all have to be there for it to be considered a Field Definition. */

;	Definition		= Case ( hasTableDefinition
					;	Let ([b=""
						;	$_log = ""
						;	$_output = ""
						;	$_iterator = ""
						;	$_context = JSONSetElement ( "{}"
								;[ "table" ; JSON.defaultTo ( JSONGetElement ( Definition ; "_table" ) ; "" ) ; JSONString ]
								)
						];
							JSONDeleteElement ( Definition ; "_table" )
						)
					; Definition
					)

;	$_iterator		= Case ( IsEmpty ( $_iterator )
					;	JSONSetElement ( "{}"
							;[ "iteration" ; 1 ; JSONNumber ]
							;[ "stopAt" ; Case (0;0
								;	isFieldDefinition
								;		JSON.defaultTo ( JSONGetElement ( Definition ; "reps" ) ; "" )
								;	ValueCount ( properties ) - hasTableDefinition
								) ; JSONNumber ]
							)
					; $_iterator
					)

;	Iterator			= $_iterator


;	$_log			= Case ( debug ; List ( $_log ; "---------" ; JSONSetElement ( "{}"
					;[ "properties" ; properties ; JSONString ]
					;[ "isFieldDefinition" ; isFieldDefinition ; JSONBoolean ]
					;[ "Iterator" ; Iterator ; JSONObject ]
					;[ "output" ; $_output ; JSONObject ]
					)))


;	iteration			= JSON.defaultTo ( JSONGetElement ( Iterator ; "iteration" ) ; "" )

;	dig			= Case (0;0
				;	isFieldDefinition
				;		Let ([b=""
						;	table			= JSON.defaultTo ( JSONGetElement ( $_context ; "table" ) ; "" )
						;	field.name		= JSON.defaultTo ( JSONGetElement ( Definition ; "field" ); "" )
						;	field.type		= JSON.defaultTo ( JSONGetElement ( Definition ; "type" ); "" )
						;	field.reps		= JSON.defaultTo ( JSONGetElement ( Definition ; "reps" ); "" )
						;	field.rep			= iteration
						;	field.name.fullyQualified	= Substitute ( "{{table}}::{{field}}[{{rep}}]"
											;[ "{{table}}" ; Table ]
											;[ "{{field}}" ; field.name ]
											;[ "{{rep}}" ; field.rep ]
											)
						;	field.value		= Evaluate ( field.name.fullyQualified )

						;	field.jsonKey		= field.name &amp; Case ( field.reps &gt; 1 ; "." &amp; field.rep )

						;	$_log			= Case ( debug ; List ( $_log ; "      ----- " ; JSONSetElement ( "{}"
											;[ "field.name" ; field.name ; JSONString ]
											;[ "field.type" ; field.type ; JSONNumber ]
											;[ "field.reps" ; field.reps ; JSONNumber ]
											;[ "field.rep" ; field.rep ; JSONNumber ]
											;[ "field.fullyQualifiedName" ; field.name.fullyQualified ; JSONString ]
											;[ "field.jsonKey" ; field.jsonKey ; JSONString ]
											;[ "field.value" ; field.value ; JSONString ]
											)))

						;	$_output			= JSONSetElement ( $_output ; field.jsonKey ; field.value ; field.type )
						];"")
				;	Let ([b=""
					;	thisProp		= GetValue ( properties ; iteration )
					;	thisPropVal	= JSONGetElement ( Definition ; thisProp )

					;	$_log		= Case ( debug ; List ( $_log ; "  -- " ; JSONSetElement ( "{}"
									;[ "thisProp.name" ; thisProp ; JSONString ]
									;[ "thisProp.value" ; thisPropVal ; Case ( Left ( thisPropVal ; 1 ) = "{" ; JSONObject ; JSONString ) ]
									)))

					;	$_iterator	= ""
					;	dig		= JSON.record.getData ( thisPropVal )
					;	$_iterator	= Iterator
					]; "" )
				)

	/* Now iterate as necessary */
;	stopAt			= JSON.defaultTo ( JSONGetElement ( Iterator ; "stopAt" ) ; 0 )
;	iterate			= Case ( iteration &lt;  stopAt
					; Let ( $_iterator = JSONSetElement ( $_iterator ; "iteration" ; iteration + 1 ; JSONNumber )
						; JSON.record.getData ( Definition )
						)
					)
];
Case ( debug ; $_log ; $_output )
)</Calculation></CustomFunction><CustomFunction id="11" functionArity="1" visible="True" parameters="context" name="JSON.record.getFields"><Calculation>/*
 * @author Steve Allen &lt;steven.allen@rcconsulting.com&gt;
 * @purpose To provide a flexible method of consistently defining a data structure for a table such that other functions (e.g. `JSON.record.getData()`) can follow it and read data out of the record.
 *
 * @param context (object)
 *   @property *required _table (string) The name of the TO that will be used to get the field data.
 *   @property *optional config (object)
 *      @property *optional include (array) If this array is present, it will limit the fields included to only those specified therein.
 *        @member *optional field (string) The name (field only, not the fully qualified name) of the field you wish to include.
 *      @property *optional exclue (array) If this array is present, it will actively prevent inclusion of those fields specified therein.
 *        @member *optional field (string) The name (field only, not the fully qualified name) of the field you wish to exclude.
 *
 * @returns (object)
 *    @property {hierarchy-parent | Field-Definition} (obj)
 *        hiearchy-parent If field names have dots in them, they will be treated as a JSON path.  e.g. name.first.  So the hierarchy parent would have the children within that hiearchy as properties under it
 *        Field-Definition At the lowest level of the naming hierarchy, the actual definition
 *          @property field (str) The full, unqualified name of the field.
 *          @property type (number) The numeric representation of the JSON type as specified by FileMaker's built in constants.
 *             (JSONNumber | JSONString )
 *          @property reps (number) The number of repetitions defined for the field.
 */

Let ([b=""

;	context.table	= JSON.defaultTo ( JSONGetElement ( context ; "table" ) ; "" )
;	config.include	= JSON.defaultTo ( JSONGetElement ( context ; "config.include" ) ; "" )
;	config.exclude	= JSON.defaultTo ( JSONGetElement ( context ; "config.exclude" ) ; "" )

;	$record = "{}"

;	config.include	= Case ( not IsEmpty ( config.include ) ; Substitute ( config.include
					;[ "\"" ; "'" ]
					;[ "[" ; "(" ]
					;[ "]" ; ")" ]
					)
				)
;	config.exclude	= Case ( not IsEmpty ( config.exclude ) ; Substitute ( config.exclude
					;[ "\"" ; "'" ]
					;[ "[" ; "(" ]
					;[ "]" ; ")" ]
					)
				)

;	flds = Substitute ( List ( ""
		; "'	$record	= JSONSetElement ( $record'"
		; "'			;[ \"' + FieldName + '.field\" ; \"' + FieldName + '\" ; JSONString ]'"
		; "'			;[ \"' + FieldName + '.type\" ; ' +
						CASE
							WHEN FieldType LIKE 'decimal%' THEN 'JSONNumber'
							ELSE 'JSONString'
						END
				+ ' ; JSONNumber ]'"
		; "'			;[ \"' + FieldName + '.reps\" ; ' + CAST ( FieldReps AS varchar ) + ' ; JSONNumber ]'"
		; "'			)'"
		)
		;[ "¶" ; ",¶" ]
		)

;	sqlStr = "
SELECT
	" &amp; flds &amp; "

FROM FileMaker_Fields

WHERE
		TableName=?
	AND	FieldClass='Normal'
"
&amp; Case ( not IsEmpty ( config.include ) ; " AND FieldName IN " &amp; config.include )
&amp; Case ( not IsEmpty ( config.exclude ) ; " AND FieldName NOT IN " &amp; config.exclude ) 

;	sqlRes = ExecuteSQL ( sqlStr ; "¶" ; ";" ; context.table )
;	errChk = Case ( sqlRes = "?" ; "" ; "Let ([ b=\"\";" &amp; sqlRes &amp; "];$record)" )

;	json = Case ( IsValidExpression ( errChk ) ; Evaluate ( errChk ) ; "" )

];
json
)</Calculation></CustomFunction><CustomFunction id="23" functionArity="1" visible="True" parameters="json" name="JSON.setVariables"><Calculation>Case ( not IsValidExpression ( While (i=0;0;i=0;0) ) ; "?" ;

While ([b=""
/* For Testing
;	json = JSONSetElement ( "{}"
		;[ "test[0].hello" ; "world" ; JSONString ]
		;[ "test[0].ping" ; "pong" ; JSONString ]
		;[ "test[1].number" ; 1 ; JSONNumber ]
		;[ "test[1].date" ; GetAsNumber ( Get ( CurrentDate ) ) ; JSONNumber ]
		)
*/
;	path = "[]"
;	variables = ""
;	currentJson = json
]; ValueCount ( JSONListKeys ( json ; "" ) )
;[b=""
;	jsonPath = Substitute ( JSONListValues ( path ; "" ) ; "¶" ; "." )

;	currentJson = JSONGetElement ( json ; jsonPath )

;	currentJson.isValue	= Left ( JSONFormatElements ( currentJson ) ; 1 ) = "?"
;	currentJson.isArray	= (not currentJson.isValue) and Left ( currentJson ; 1 ) = "["
;	currentJson.isObject	= (not currentJson.isValue) and Left ( currentJson ; 1 ) = "{"
;	currentJson.isDone	= (currentJson.isArray or currentJson.isObject) and Length ( currentJson ) = 2

;	variables = Case (0;0
			;	currentJson.isValue
			;		Let ([b=""
					;	variable = "$" &amp; Substitute ( jsonPath ; [ "[" ; "" ] ; [ "]" ; "" ] )
					;	evalStr	= "Let(" &amp; variable &amp; "=" &amp; Quote ( currentJson ) &amp; ";\"\")"
					;	set = Evaluate ( evalStr )
					];
						List ( variables ; variable )
					)
			;	variables
			)

;	path	= Case (0;0
			;	currentJson.isValue or currentJson.isDone
			;		Let ([b=""
					;	pathkeys = JSONListKeys ( path ; "" )
					;	latestPathKey = GetValue ( pathkeys ; ValueCount ( pathkeys ) )
					];
						JSONDeleteElement ( path ; latestPathKey )
					)
			;	currentJson.isObject or currentJson.isArray
			;		Let ([b=""
					;	pathkeys = JSONListKeys ( path ; "" )
					;	latestPathKey = GetValue ( pathkeys ; ValueCount ( pathkeys ) )
					;	nextKey = GetValue ( JSONListKeys ( currentJson ; "" ) ; 1 )
					;	nextKey = Case ( currentJson.isArray ; "[" &amp; nextKey &amp; "]" ; nextKey )
					];
						JSONSetElement ( path ; "[" &amp; latestPathKey + 1 &amp; "]" ; nextKey ; JSONString )
					)
			;	path
			)
;	json	= Case (0;0
			;	currentJson.isValue or currentJson.isDone
			;		JSONDeleteElement ( json ; jsonPath )
			;	json
			)
];
variables
) )</Calculation></CustomFunction><CustomFunction id="43" functionArity="1" visible="True" parameters="JSON.usedTimeBlocks" name="JSON.getAvailableTimeBlocks"><Calculation>Let ([b=""
//;	JSON.usedTimeBlocks = "UseBuiltInTestData"
//;	JSON.usedTimeBlocks = $$blocks
;	blocks = Case ( JSON.usedTimeBlocks = "UseBuiltInTestData" ; JSONSetElement ( "[]"

			;[ 0 ; JSONSetElement ( "{}"
				//;[ "block" ; 6 ; JSONNumber ]
				;[ "start" ; Time ( 11 ; 0 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 11 ; 45 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
			;[ 1 ; JSONSetElement ( "{}"
				//;[ "block" ; 2 ; JSONNumber ]
				;[ "start" ; Time ( 8 ; 15 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 9 ; 30 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
			;[ 2 ; JSONSetElement ( "{}"
				//;[ "block" ; 1 ; JSONNumber ]
				;[ "start" ; Time ( 8 ; 0 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 9 ; 15 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
			;[ 3 ; JSONSetElement ( "{}"
				//;[ "block" ; 4 ; JSONNumber ]
				;[ "start" ; Time ( 10 ; 30 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 11 ; 30 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
			;[ 4 ; JSONSetElement ( "{}"
				//;[ "block" ; 7 ; JSONNumber ]
				;[ "start" ; Time ( 11 ; 0 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 12 ; 0 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
			;[ 5 ; JSONSetElement ( "{}"
				//;[ "block" ; 5 ; JSONNumber ]
				;[ "start" ; Time ( 11 ; 0 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 11 ; 15 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
			;[ 6 ; JSONSetElement ( "{}"
				//;[ "block" ; 3 ; JSONNumber ]
				;[ "start" ; Time ( 10 ; 0 ; 0 ) ; JSONString ]
				;[ "end" ; Time ( 11 ; 0 ; 0 ) ; JSONString ]
				) ; JSONObject ]
 
		) ; JSON.usedTimeBlocks )

;	blocksData    = JSONListValues ( blocks ; "" )
;	blocksQty    = ValueCount ( JSONListKeys ( blocks ; "" ) )

;	blocksSorted	= While([b=""
				;	block = 1
				;	blockData = ""
				;	start = ""
				;	end = ""
				;	times = ""
				];
					block &lt;= blocksQty
				;[b=""
			
				;	blockData = GetValue ( blocksData ; block )

				;	start    = GetAsNumber ( GetAsTime ( JSONGetElement ( blockData ; "start" ) ) )
				;	end    = GetAsNumber ( GetAsTime ( JSONGetElement ( blockData ; "end" ) ) )

				;	times = List ( times ; start &amp; "." &amp; end )

				;	block = block + 1
				]; SortValues ( UniqueValues ( times ; 2 ) ; 2 ) )

;	usedTimes	= While ([b=""
				;	block = 1
				;	blockData = ""
				;	blockData.split = ""
				;	start = 0
				;	end = 0
				;	previousStart = 0
				;	previousEnd = 0
				;	keepBlock = False
				;	maxEnd = GetAsNumber ( GetAsTime ( Time ( 0 ; 0 ; 0 ) ) )
				;	starts = ""
				;	times = "{}"
				];
					block &lt;= blocksQty
				;[b=""
 				;	blockData = GetValue ( blocksSorted ; block )
				;	blockData.split = Substitute ( blockData ; "." ; "¶" )

				;	start    = +GetValue ( blockData.split ; 1 )
				;	end    = +GetValue ( blockData.split ; 2 )

				;	previousStart = 0+GetValue ( SortValues ( JSONListKeys ( times ; "" ) ; -2 ) ; 1 )
				;	previousEnd = 0+JSONGetElement ( times ; previousStart )

				;	overlapsPrevious = start &lt;= previousEnd
				;	extendsPrevious = maxEnd &lt; end
				;	maxEnd = Case ( extendsPrevious ; end ; maxEnd )

				;	times = Case (0;0

						;	overlapsPrevious and extendsPrevious
						;		JSONSetElement ( times
								;[ previousStart ; end ; JSONString ]
								)

						;	extendsPrevious
						;		JSONSetElement ( times
								;[ start ; end ; JSONString ]
								)

						;	times
						
						)

				;	block = block + 1
				]; times )

;	dayStart	= Quote ( GetAsNumber ( Time ( 0 ; 0 ; 0 ) ) )
;	dayEnd		= Quote ( GetAsNumber ( Time ( 23 ; 59 ; 59 ) ) )

;	firstStart	= Quote ( 0+GetValue ( JSONListKeys ( usedTimes ; "" ) ; 1 ) )
;	lastEnd		= Quote ( 0+GetValue ( SortValues ( JSONListValues ( usedTimes ; "" ) ; -2 ) ; 1 ) )

;	availableTimes	= Substitute ( usedTimes
				;[ "{" &amp; firstStart &amp; ":"
					; Case (0;0
						;	+firstStart &gt; +dayStart
						;		"{" &amp; dayStart &amp; "," &amp; firstStart &amp; ":"
						;	"{"
						)]
				;[ ":" &amp; lastEnd &amp; "}"
					; Case (0;0
						;	+dayEnd &gt; +lastEnd
						;		":" &amp; lastEnd &amp; "," &amp; dayEnd &amp; "}"
						;	"}"
						)]

				;[ ":" ; "~" ]
				;[ "," ; ":" ]
				;[ "~" ; "," ]
				)
;	availableTimesStarts	= JSONListKeys ( availableTimes ; "" )
;	availableTimesEnds	= JSONListValues ( availableTimes ; "" )
;	availableTimesQty	= ValueCount ( JSONListKeys ( availableTimes ; "" ) )

;	availableBlocks = While ([b=""
				;	block = 1
				;	blockData = ""
				;	times = "[]"
				];
					block &lt;= availableTimesQty
				;[b=""
				;	times = JSONSetElement ( times ; block - 1 ; JSONSetElement ( "{}"
						;[ "start" ; Right ( "0" &amp; GetAsTime ( +GetValue ( availableTimesStarts ; block ) ) ; 8 ) ; JSONString ]
						;[ "end" ; Right ( "0" &amp; GetAsTime ( +GetValue ( availableTimesEnds ; block ) ) ; 8 ) ; JSONString ]
						) ; JSONObject )

				;	block = block + 1
				]; times )


];
availableBlocks
)</Calculation></CustomFunction><CustomFunction id="3" functionArity="1" visible="True" parameters="emlText" name="GetEMLFieldsAsJSON"><Calculation>While ([~=""
;	emlText = Case ( PatternCount ( emlText ; "¶-" ) ; Left ( emlText ; Position ( emlText ; "¶-" ; 1; 1 ) - 1 ) ; emlText )

;	lineCount = ValueCount ( emlText )

;	thisLine.value = ""
;	thisLine.number = 0

;	lines = "[]"
;	fields = "{}"

;	thisField.name = ""
;	thisField.value = ""
;	thisField.value.line = 0

;	continue = True
]; continue ;[~=""

;	thisLine.number = thisLine.number + 1
;	thisLine.value      = GetValue ( emlText ; thisLine.number )

;	lines = JSONSetElement ( lines ; thisLine.number - 1 ; thisLine.value ; JSONString )

;	thisLine.firstChar = Code ( Left ( thisLine.value ; 1 ) )

	/* per IETF RFC 5322 2.2 */
;	thisLine.startsNewField = 33 &lt;= thisLine.firstChar and thisLine.firstChar &lt;= 126

;	fields = Case (0;0
		;		thisLine.startsNewField
			and	(not IsEmpty ( thisField.name ))
			and	(not IsEmpty ( thisField.value ))
		;		Choose ( thisField.value.line &gt; 1
				;	JSONSetElement ( fields ; thisField.name ; JSONGetElement ( thisField.value ; 0 ) ; JSONString )
				;	JSONSetElement ( fields ; thisField.name ; thisField.value ; JSONArray )
				)
		;	fields
		)

;	thisLine.value = Case (0;0
		;	thisLine.startsNewField
		;		Substitute ( thisLine.value ; ": " ; "¶" )
		;	thisLine.value
		)

;	thisField.name = Case (0;0
		;	thisLine.startsNewField
		;		GetValue ( thisLine.value ; 1 )
		;	thisField.name
		)

;	thisField.value.line = Case ( thisLine.startsNewField ; 1 ; thisField.value.line + 1 )

;	thisField.value = JSONSetElement ( Case ( thisLine.startsNewField ; "[]" ; thisField.value )
		;	thisField.value.line - 1
		;	Trim ( GetValue ( thisLine.value ; 1 + thisLine.startsNewField ) )
		;	JSONString
		)

;	continue =		thisLine.number &lt;= lineCount
			and	thisLine.firstChar &lt;&gt; Code ( "-" ) /* we aren't starting a Part */
];

JSONSetElement ( "{}"
//;[ "emlText" ; emlText ; JSONString ]
;[ "lineCount" ; lineCount ; JSONNumber ]
;[ "lines" ; lines ; JSONArray ]
;[ "fields" ; fields ; JSONObject ]
)

)</Calculation></CustomFunction><CustomFunction id="5" functionArity="2" visible="True" parameters="json;path" name="JSONGetValues"><Calculation>Let ([~=""

;	v = JSONListValues ( json ; path )
];
	Case (0;0
	;	IsEmpty ( v )
	;		JSONGetElement ( json ; path )
	;	v
	)
)</Calculation></CustomFunction></fmxmlsnippet></Clipboard><Picture></Picture></FMClipboard>